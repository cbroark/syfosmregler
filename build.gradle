import no.nav.codegen.CodegenTask

buildscript {
    ext.coroutines_version = '1.0.0'
    ext.cxf_version = '3.2.7'
    ext.jackson_version = '2.9.7'
    ext.jaxb_api_version = '2.1'
    ext.jaxb_basic_ant_version = '1.11.1'
    ext.jaxb_version = '2.3.0.1'
    ext.javax_activation_version = '1.1.1'
    ext.javax_jaxws_api_version = '2.2.1'
    ext.kluent_version = '1.39'
    ext.ktor_version = '1.1.2'
    ext.logback_version = '1.2.3'
    ext.logstash_encoder_version = '5.1'
    ext.prometheus_version = '0.5.0'
    ext.saaj_version = "1.4.0"
    ext.spek_version = "2.0.0-rc.1"
    ext.sykmelding_version = "1.1-SNAPSHOT"
    ext.nav_person_v3_version = '3.2.0'
    ext.jfairy_version = '0.6.2'
}

plugins {
    id 'no.nils.wsdl2java' version "0.10"
    id 'org.jmailen.kotlinter' version '1.14.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id("com.diffplug.gradle.spotless") version "3.14.0"
    id("com.github.johnrengelman.shadow") version "4.0.3"
}

group 'no.nav.syfo'
version '1.0.10'

jar.manifest {
    attributes("Main-Class" : "no.nav.syfo.BootstrapKt")
}

shadowJar {
    mergeServiceFiles("META-INF/cxf")
}

wsdl2java {
    wsdlDir = file("$projectDir/src/main/resources/wsdl")
    wsdlsToGenerate = [["-xjc", "-b", "$projectDir/src/main/resources/xjb/binding.xml", "$projectDir/src/main/resources/wsdl/helsepersonellregisteret.wsdl"]]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://repo.adeo.no/repository/maven-releases/' }
    maven { url 'https://repo.adeo.no/repository/maven-snapshots/' }
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        showStandardStreams = true
    }
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources"
        }
    }
}

task generateDiagnoseCodes(type: CodegenTask) {
    outputDirectory = file("${project.buildDir}/generated-sources")
}

compileKotlin {
    dependsOn generateDiagnoseCodes, wsdl2java
    kotlinOptions.jvmTarget = "1.8"
}


dependencies {
    wsdl2java "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    wsdl2java "javax.xml.bind:jaxb-api:$jaxb_api_version"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$jaxb_basic_ant_version"
    wsdl2java "javax.activation:activation:$javax_activation_version"
    wsdl2java "javax.xml.ws:jaxws-api:$javax_jaxws_api_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "io.prometheus:simpleclient_common:$prometheus_version"

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"

    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-auth-basic:$ktor_version"
    implementation "io.ktor:ktor-client-jackson:$ktor_version"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:$nav_person_v3_version"

    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "javax.xml.ws:jaxws-api:$javax_jaxws_api_version"
    implementation "com.sun.xml.messaging.saaj:saaj-impl:$saaj_version"

    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "com.devskiller:jfairy:$jfairy_version"

    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    implementation "javax.xml.bind:jaxb-api:$jaxb_api_version"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    implementation "javax.activation:activation:$javax_activation_version"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task printVersion() {
    doLast {
        println(version)
    }
}
