import no.nav.codegen.CodegenTask

buildscript {
    ext.coroutines_version = '1.0.0'
    ext.fellesformat_version = '1.0'
    ext.jackson_version = '2.9.6'
    ext.jaxb_api_version = '2.1'
    ext.jaxb_basic_ant_version = '1.11.1'
    ext.jaxb_version = '2.3.0.1'
    ext.javax_activation_version = '1.1.1'
    ext.kith_hodemelding_version = '1.1'
    ext.kith_apprec_version = '1.1'
    ext.kluent_version = '1.39'
    ext.ktor_version = '1.0.0'
    ext.logback_version = '1.2.3'
    ext.logstash_encoder_version = '5.1'
    ext.prometheus_version = '0.5.0'
    ext.spek_version = "2.0.0-rc.1"
    ext.nav_person_v3_version = '3.2.0'
    ext.cxf_version = '3.2.5'
    ext.javax_jaxws_api_version = '2.2.1'
    ext.saaj_version = "1.4.0"

    ext.jaxb2SourceDir = file("$buildDir/generated-sources/jaxb2")
}

plugins {
    id 'no.nils.wsdl2java' version "0.10"
    id 'org.jmailen.kotlinter' version '1.14.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id("com.diffplug.gradle.spotless") version "3.14.0"
    id 'application'
}

group 'no.nav.syfo'
version '1.0-SNAPSHOT'

mainClassName = 'no.nav.syfo.BootstrapKt'

wsdl2java {
    wsdlDir = file("$projectDir/src/main/resources/wsdl")
    wsdlsToGenerate = [["$projectDir/src/main/resources/wsdl/helsepersonellregisteret.wsdl"]]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://repo.adeo.no/repository/maven-releases/' }
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
}

applicationDefaultJvmArgs = [
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
        '-Djavax.net.ssl.trustStoreType=JKS'
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        showStandardStreams = true
    }
}

configurations {
    generateJaxb2
}

class Jaxb2Task extends DefaultTask {
    @OutputDirectory
    File outputDir
    @InputDirectory
    File sourceDir

    @TaskAction
    def generateFiles() {
        outputDir.deleteDir()
        outputDir.mkdirs()
        ant.taskdef(
                'classpath': project.configurations.generateJaxb2.asPath,
                'name': 'xjc',
                'classname': 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        )

        ant.xjc(
                'destdir': outputDir,
                'package': 'no.nav.model.sm2013',
                'schema': "$sourceDir/HelseOpplysningerArbeidsuforhet_2013-10-01.xsd",
                'encoding': 'UTF-8',
                'fork': 'true'
        )
        ant.xjc(
                'destdir': outputDir,
                'package': 'no.nav.model.kith',
                'schema': "$sourceDir/kith.xsd",
                'encoding': 'UTF-8',
                'fork': 'true'
        )
        ant.xjc(
                'destdir': outputDir,
                'package': 'no.nav.model.felleskomponent1',
                'schema': "$sourceDir/felleskomponent1.xsd",
                'encoding': 'UTF-8',
                'fork': 'true'
        )

    }
}

task generateJaxb2(type: Jaxb2Task) {
    outputDir = jaxb2SourceDir
    sourceDir = file("$projectDir/src/main/resources/xsd")
}

sourceSets {
    main {
        java {
            srcDir jaxb2SourceDir
            srcDir "$buildDir/generated-sources"
        }
    }
}

sourceSets["main"].java.srcDirs()

task generateDiagnoseCodes(type: CodegenTask) {
    outputDirectory = file("${project.buildDir}/generated-sources")
}

compileKotlin {
    dependsOn generateJaxb2, generateDiagnoseCodes, wsdl2java
    kotlinOptions.jvmTarget = "1.8"
}


dependencies {
    generateJaxb2 "com.sun.xml.bind:jaxb-xjc:$jaxb_version"
    generateJaxb2 "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    generateJaxb2 "javax.xml.bind:jaxb-api:$jaxb_api_version"
    generateJaxb2 "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$jaxb_basic_ant_version"
    generateJaxb2 "javax.activation:activation:$javax_activation_version"

    wsdl2java "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    wsdl2java "javax.xml.bind:jaxb-api:$jaxb_api_version"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$jaxb_basic_ant_version"
    wsdl2java "javax.activation:activation:$javax_activation_version"
    wsdl2java "javax.xml.ws:jaxws-api:$javax_jaxws_api_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "io.prometheus:simpleclient_common:$prometheus_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "no.nav.syfo.tjenester:fellesformat:$fellesformat_version"
    implementation "no.nav.syfo.tjenester:kith-hodemelding:$kith_hodemelding_version"
    implementation "no.nav.syfo.tjenester:kith-apprec:$kith_apprec_version"

    implementation "no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:$nav_person_v3_version"

    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "javax.xml.ws:jaxws-api:$javax_jaxws_api_version"
    implementation "com.sun.xml.messaging.saaj:saaj-impl:$saaj_version"

    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"

    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    implementation "no.nav.syfo.tjenester:fellesformat:$fellesformat_version"
    implementation "no.nav.syfo.tjenester:kith-hodemelding:$kith_hodemelding_version"

    implementation "javax.xml.bind:jaxb-api:$jaxb_api_version"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    implementation "javax.activation:activation:$javax_activation_version"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task printVersion() {
    doLast {
        println(version)
    }
}
